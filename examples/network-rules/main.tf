module "naming" {
  source  = "codectl/naming/azure"
  version = "~> 0.1"

  suffix = ["demo", "dev"]
}

module "regions" {
  source  = "codectl/locations/azure"
  version = "~> 1.0"

  location = {
    primary = "swedencentral"
  }
}

module "rg" {
  source  = "codectl/rg/azure"
  version = "~> 1.0"

  groups = {
    demo = {
      name     = module.naming.resource_group.name_unique
      location = module.regions.location.primary.name
    }
  }
}

module "network" {
  source  = "codectl/vnet/azure"
  version = "~> 1.0"

  vnet = {
    name                = module.naming.virtual_network.name
    location            = module.rg.groups.demo.location
    resource_group_name = module.rg.groups.demo.name
    address_space       = ["10.20.0.0/16"]

    subnets = {
      sn1 = {
        name              = module.naming.subnet.name
        address_prefixes  = ["10.20.1.0/24"]
        service_endpoints = ["Microsoft.CognitiveServices"]
      }
    }
  }
}

module "cognitiveservices" {
  source  = "codectl/cog/azure"
  version = "~> 1.0"

  account = {
    name                  = module.naming.cognitive_account.name_unique
    resource_group_name   = module.rg.groups.demo.name
    location              = module.rg.groups.demo.location
    kind                  = "OpenAI"
    custom_subdomain_name = module.naming.cognitive_account.name_unique

    network_acls = {
      default_action = "Deny"
      bypass         = "AzureServices"
      ip_rules       = ["203.0.113.0/24"]
      virtual_network_rules = {
        sn1 = {
          subnet_id = module.network.subnets.sn1.id
        }
      }
    }
  }
}
